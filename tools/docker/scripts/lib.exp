#!/usr/bin/expect
#
# Advanced Forwarding Interface : AFI client examples
#
# Created by Sandesh Kumar Sodhi, December 2017
# Copyright (c) [2017] Juniper Networks, Inc. All rights reserved.
#
# All rights reserved.
#
# Notice and Disclaimer: This code is licensed to you under the Apache
# License 2.0 (the "License"). You may not use this code except in compliance
# with the License. This code is not an official Juniper product. You can
# obtain a copy of the License at http://www.apache.org/licenses/LICENSE-2.0
#
# Third-Party Code: This code may depend on other components under separate
# copyright notice and license terms. Your use of the source code for those
# components is subject to the terms and conditions of the respective license
# as noted in the Third-Party source code file.
#

package require tdom

exp_internal -f exp_internal.log.txt 0;

#
# These VCP and VFP console port must match with 
# console port configured in cfg/vmx.conf
#
set debug_level            1
set vcp_prompt             "#"
set vfp_root_password      "root"
set vfp_prompt             "#"
set re_cli_prompt          "root>"
set re_cli_config_prompt   "root#"
set host_prompt             "#"
set vmx_config_file        "/root/JP4Agent/tools/config/vmx-cfg.xml"

#
# Read configuration
#
set fd [open $vmx_config_file]
set xml [read $fd]
set doc [dom parse $xml]
set root [$doc documentElement]

set vcpNode [$root selectNodes "/vmxConfig/vcp"]
set vcp_ip [$vcpNode getAttribute  IP]

set vcpNode [$root selectNodes "/vmxConfig/vcp/username/text()"]
set vcp_username [$vcpNode nodeValue]

set vcpNode [$root selectNodes "/vmxConfig/vcp/password/text()"]
set vcp_password [$vcpNode nodeValue]

set vcpNode [$root selectNodes "/vmxConfig/vcp/port/text()"]
set vcp_console_port [$vcpNode nodeValue]

set vfpNode [$root selectNodes "/vmxConfig/vfp"]
set vfp_ip [$vfpNode getAttribute  IP]

set vfpNode [$root selectNodes "/vmxConfig/vfp/username/text()"]
set vfp_username [$vfpNode nodeValue]

set vfpNode [$root selectNodes "/vmxConfig/vfp/password/text()"]
set vfp_password [$vfpNode nodeValue]

set vfpNode [$root selectNodes "/vmxConfig/vfp/port/text()"]
set vfp_console_port [$vfpNode nodeValue]

match_max 100

proc log_debug {str} {
    global debug_level
    if {$debug_level != 0} {
        puts "\r\[DEBUG\]___________\[script $::argv0 log\] $str"
    }
}

proc log_error {str} {
    puts "\r\[ERROR\]___________\[script $::argv0 log\] $str"
}

#
# Host shell
#
proc host_shell {} {
    global host_prompt
    spawn /bin/bash
    if {$spawn_id == 0} {
        log_error "Hosh bash spawn_id $spawn_id returning"
        set rez [display_usage $script_name ]
        exit 1
    }

    expect "$host_prompt" { send "\r" }
    expect "$host_prompt" { send "\r" }
    log_debug "The output is '$expect_out(buffer)'."
    set host_prompt [lindex [split $expect_out(buffer) "\n"] end]
    log_debug "The prompt is '$host_prompt'"

    return $spawn_id
}


#
# Login to VCP console
#
proc login_vcp_console {} {
    global vcp_prompt
    global vcp_console_port
    global vcp_username
    global vcp_password
    global re_cli_prompt
    global re_cli_config_prompt
    #
    # Set 6000 seconds timeout to wait for VCP to come up, if not already up
    #
    set timeout 6000

    spawn /usr/bin/telnet localhost $vcp_console_port
    if {$spawn_id == 0} {
        log_error "Telnet spawn_id $spawn_id returning"
        set rez [display_usage $script_name ]
        exit 1
    }

    log_debug "Telnet spawn_id $spawn_id"

    send "\r"
    send "\r"

    #
    # While loop to take care of 
    # boot menu appearing multiple times
    #
    while true {
        expect {
            "Connection refused" {
                log_error "Telnet didn't succeed!!"
                exit 1
            }
            "Choice:" {
                log_debug "Got boot menu"
                #
                # Choice 1
                #
                send "1\r"
                #
                # loop (while) again
                #
            }
            "ogin:" {
                log_debug "need to login"
                send "$vcp_username\r"
                expect {
                    "assword:" {
                        log_debug "Entering password"
                        set re_cli_prompt          "root@vcp-vm>"
                        set re_cli_config_prompt   "root@vcp-vm#"
                        send "$vcp_password\r"
                        expect {
                            "#" {
                                log_debug "got prompt after login (with password)"
                                send "ls -l\r"
                                #
                                # break out of loop
                                #
                                break
                            }
                            timeout {
                                log_error "login timeout (password entered)"
                                close
                                exit 1
                            }
                        }
                    }
                    "#" {
                        log_debug "got prompt after login (passwordless)"
                        send "ls -l\r"
                        #
                        # break out of loop
                        #
                        break
                    }
                    timeout {
                        log_error "login timeout"
                        close
                        exit 1
                    }
                }
            }
            eof  {
                log_error "telnet:eof"
                exit 1
            }
            timeout {
                log_error "telnet: timeout"
                close
                exit 1
            }
        }
    }

    expect "$vcp_prompt" { send "\r" }
    expect "$vcp_prompt" { send "\r" }
    log_debug "login_vcp_console: The output is '$expect_out(buffer)'."
    set vcp_prompt [lindex [split $expect_out(buffer) "\n"] end]
    log_debug "login_vcp_console: The prompt is '$vcp_prompt'"

    return $spawn_id
}

#
# ssh to VCP
#
proc login_vcp_ssh {} {
    global vcp_prompt
    global vcp_ip
    global vcp_username
    global vcp_password
    global re_cli_prompt
    global re_cli_config_prompt
    set re_cli_prompt          "root@vcp-vm>"
    set re_cli_config_prompt   "root@vcp-vm#"
    #
    # Set 10 seconds 
    #
    set timeout 10

    spawn /usr/bin/ssh -oStrictHostKeyChecking=no $vcp_username@$vcp_ip
    if {$spawn_id == 0} {
        log_error "ssh spawn_id $spawn_id returning"
        set rez [display_usage $script_name ]
        exit 1
    }

    log_debug "ssh spawn_id $spawn_id"

    send "\r"
    send "\r"

    expect {
        "assword:" {
            log_debug "Entering password"
            send "$vcp_password\r"
            expect {
                "#" {
                    log_debug "got prompt after login (with password)"
                    send "ls -l\r"
                }
                timeout {
                    log_error "login timeout (password entered)"
                    close
                    exit 1
                }
            }
        }
        eof  {
            log_error "telnet:eof"
            exit 1
        }
        timeout {
            log_error "login timeout"
            close
            exit 1
        }
    }

    expect "$vcp_prompt" { send "\r" }
    expect "$vcp_prompt" { send "\r" }
    log_debug "login_vcp_ssh: The output is '$expect_out(buffer)'."
    set vcp_prompt [lindex [split $expect_out(buffer) "\n"] end]
    log_debug "login_vcp_ssh: The prompt is '$vcp_prompt'"

    return $spawn_id
}


#
# cp file to VCP
#
proc cp_to_vcp {src dst} {
    global vcp_ip
    global vcp_username
    global vcp_password
    #
    # Set 120 seconds 
    #
    set timeout 120

    send "scp -oStrictHostKeyChecking=no $src $vcp_username@$vcp_ip:$dst \r"

    expect {
        "assword:" {
            log_debug "cp_to_vcp: Entering password"
            send "$vcp_password\r"
        }
        eof  {
            log_error "cp_to_vcp: eof"
            exit 1
        }
        timeout {
            log_error "cp_to_vcp: timeout"
            exit 1
        }
    }
}


#
# Login to VFP console
#
proc login_vfp_console {} {
    global vfp_prompt
    global vfp_console_port
    global vfp_root_password
    #
    # Set 600 seconds timeout to wait for VFP to come up, if not already up
    #
    set timeout 600

    spawn /usr/bin/telnet localhost $vfp_console_port
    if {$spawn_id == 0} {
        log_error "Telnet spawn_id $spawn_id returning"
        set rez [display_usage $script_name ]
        exit 1
    }

    log_debug "Telnet spawn_id $spawn_id"

    send "\r"
    send "\r"
    expect {
        "Connection refused" {
            log_error "Telnet didn't succeed!!"
            exit 1
        }
        "ogin:" {
            log_debug "need to login"
            send "root\r"
            expect {
                "assword:" {
                    log_debug "Entering password"
                    send "$vfp_root_password\r"
                    expect {
                        "#" {
                            log_debug "got prompt after login"
                            send "ls -l\r"
                        }
                        timeout {
                            log_error "login timeout (password entered)"
                            close
                            exit 1
                        }
                    }
                }
                timeout {
                    log_error "login timeout (waiting for password prompt)"
                    close
                    exit 1
                }
            }
        }
        "#" {
            log_debug "got prompt"
            send "ls -l\r"
        }
        eof  {
            log_error "telnet:eof"
            exit 1
        }
        timeout {
            log_error "telnet: timeout"
            close
            exit 1
        }
    }

    send "\r"
    send "\r"
    expect {
        "#" {
            log_debug "got prompt"
            send "\r"
            send "ls -l\r"
        }
        timeout {
            log_error "timeout_________"
            close
            exit 1
        }
    }

    expect "$vfp_prompt" { send "\r" }
    expect "$vfp_prompt" { send "\r" }
    log_debug "login_vfp_console: The output is '$expect_out(buffer)'."
    set vfp_prompt [lindex [split $expect_out(buffer) "\n"] end]
    log_debug "login_vfp_console: The prompt is '$vfp_prompt'"

    return $spawn_id
}

#
# ssh to VFP
#
proc login_vfp_ssh {} {
    global vfp_prompt
    global vfp_ip
    global vfp_username
    global vfp_password
    global vfp_root_password
    #
    # Set 10 seconds 
    #
    set timeout 10

    spawn /usr/bin/ssh -oStrictHostKeyChecking=no $vfp_username@$vfp_ip
    if {$spawn_id == 0} {
        log_error "ssh spawn_id $spawn_id returning"
        set rez [display_usage $script_name ]
        exit 1
    }

    log_debug "ssh spawn_id $spawn_id"

    send "\r"
    send "\r"

    expect {
        "assword:" {
            log_debug "Entering password"
            send "$vfp_password\r\r"
            expect {
                "$ " {
                    log_debug "got prompt $ after login (with password)"
                    send "su\r"

                    expect {
                        "assword:" {
                            log_debug "Entering root password"
                            send "$vfp_root_password\r"
                            expect {
                                "#" {
                                    log_debug "got prompt # after su (with password)"
                                    send "ls -l\r"
                                }
                                timeout {
                                    log_error "su timeout (password entered)"
                                    close
                                    exit 1
                                }
                            }
                        }
                        eof  {
                            log_error "(su)telnet:eof"
                            exit 1
                        }
                        timeout {
                            log_error "su timeout"
                            close
                            exit 1
                        }
                    }
                }
                timeout {
                    log_error "$vfp_username login timeout (password entered)"
                    close
                    exit 1
                }
            }
        }
        eof  {
            log_error "telnet:eof"
            exit 1
        }
        timeout {
            log_error "ssh timeout"
            close
            exit 1
        }
    }

    expect "$vfp_prompt" { send "\r" }
    expect "$vfp_prompt" { send "\r" }
    log_debug "login_vfp_ssh: The output is '$expect_out(buffer)'."
    set vfp_prompt [lindex [split $expect_out(buffer) "\n"] end]
    log_debug "login_vfp_ssh: The prompt is '$vfp_prompt'"

    return $spawn_id
}

